Use DataDict.pkg

//importar los archivos donde tenemos las funciones principales de validacion
Use ValidarDNI.pkg
Use ValidarEmail.pkg

Open student
Open schoolhouse

Object oValidationCounty is a DescriptionValidationTable
    Procedure Fill_List
        Forward Send Fill_List
        Send Add_Table_Value "Huelva" "HUELVA"
        Send Add_Table_Value "Sevilla" "SEVILLA"
        Send Add_Table_Value "Málaga" "MÁLAGA"
        Send Add_Table_Value "Cadiz" "CÁDIZ"
        Send Add_Table_Value "Córdoba" "CÓRDOBA"
        Send Add_Table_Value "Jaén" "JAÉN"
        Send Add_Table_Value "Almería" "ALMERÍA"
        Send Add_Table_Value "Granada" "GRANADA"
    End_Procedure
End_Object

//registrar las funciones que luego usaremos para validar email y dni
Register_Function zValidar_Email Integer iField Returns Boolean
Register_Function zValidar_Dni Integer iField   Returns Boolean

Class cstudentDataDictionary is a DataDictionary
    
    Procedure Construct_Object
        Forward Send Construct_Object
        Set Main_File to student.File_Number

        Set Add_Server_File to schoolhouse.File_Number

        Set Foreign_Field_Option DD_KEYFIELD DD_NOPUT to True
        Set Foreign_Field_Option DD_KEYFIELD DD_FINDREQ to True
        Set Foreign_Field_Option DD_INDEXFIELD DD_NOPUT to True
        Set Foreign_Field_Option DD_DEFAULT DD_DISPLAYONLY to True

        Set Key_Field_State Field student.ID to True
        Set Field_Option Field student.ID DD_DISPLAYONLY to True

        Set Field_Option Field student.ModifiedTime DD_DISPLAYONLY to True

        Set Field_Option Field student.CreatedTime DD_DISPLAYONLY to True

//una vez verificado que el email y el dni son correctos se almacena en el campo correspondiente de la bbdd        
        Set Field_Validate_msg Field student.Email to get_zValidar_Email

        Set Field_Mask_Type Field student.County to Mask_DateTime_Window
        Set Field_Value_Table Field student.County to oValidationCounty
        Set Field_Validate_msg Field student.DNI to   get_zValidar_Dni

    End_Procedure

//llamamos a las funciones definidas en los archivos para validar los email y dni

    //llamar a la funcion para validar el email
    Function zValidar_Email Integer iField Returns Boolean
        Boolean bRet
        String sEmail
        
        Get Field_Current_Value Field student.Email to sEmail
        Get ValidarEmail sEmail to bRet
        
        Function_Return (not(bRet))
        End_Function

    //llamar a la funcion para validar el dni    
    Function zValidar_Dni Integer iField Returns Boolean
        Boolean bRet
        String sDni
        
        Get Field_Current_Value Field student.DNI to sDni
        Get ValidacionDNI sDni to bRet
        
        Function_Return (not(bRet))
    End_Function

End_Class